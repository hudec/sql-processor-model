grammar org.sqlproc.model.ProcessorModel hidden(ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

generate processorModel "http://www.sqlproc.org/model/ProcessorModel"

Artifacts: {Artifacts}
    WS* (properties+=Property WS*|pojos+=PojoDefinition WS*|tables+=TableDefinition WS*
    	|procedures+=ProcedureDefinition WS*|functions+=FunctionDefinition WS*
    	|pojoPackages+=PackageDeclaration WS*
    )*;

DatabaseCatalogAssignement: dbCatalog=PropertyValue;
DatabaseSchemaAssignement: dbSchema=PropertyValue;
DatabaseTypeAssignement: dbType=IDENT;
DatabaseMetaInfoAssignement: dbMetaInfo=PropertyValue;
DriverMetaInfoAssignement: dbDriverInfo=PropertyValue;
DriverMethodOutputAssignement: driverMethod=IDENT '->' callOutput=PropertyValue;
SqlTypeAssignement: typeName=IDENT (LPAREN size=NUMBER RPAREN)? '->' type=PojoType;
ColumnTypeAssignement: dbColumn=IDENT '->' type=PojoType;
ShowColumnTypeAssignement: dbColumn=IDENT '->' type=PropertyValue;
TableAssignement: dbTable=IDENT '->' newName=IDENT;
JoinTableAssignement: dbTable=IDENT ('->' dbTables+=IDENT)+;
ColumnAssignement: dbColumn=IDENT '->' newName=IDENT;
ImportAssignement: dbColumn=IDENT '->' pkTable=IDENT ('->' pkColumn=IDENT)?;
ExportAssignement: dbColumn=IDENT '->' fkTable=IDENT ('->' fkColumn=IDENT)?;
InheritanceAssignement: discriminator=IDENT '->' dbTable=IDENT ('->' dbColumns+=IDENT)+;
ManyToManyAssignement: pkColumn=IDENT '->' pkTable=IDENT ('->' fkColumn=IDENT)?;
DebugLevelAssignement: debug=IDENT (WS+ scope=PropertyValue)?;
ProcedurePojoAssignement: dbProcedure=IDENT '->' pojo=PojoType;
FunctionPojoAssignement: dbFunction=IDENT '->' pojo=PojoType;
ImplementsAssignement: toImplement=[jvmTypes::JvmType|QualifiedName] (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?;
ExtendsAssignement: toExtends=[jvmTypes::JvmType|QualifiedName] (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?;
ImplementsAssignementGenerics: toImplement=[jvmTypes::JvmType|QualifiedName] (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?;
ExtendsAssignementGenerics: toExtends=[jvmTypes::JvmType|QualifiedName] (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?;

Property:
	(name='resolve-pojo-on'
	|name='resolve-pojo-off'
	|name='database-' database=DatabaseProperty
	|name='pojogen-' pojogen=PojogenProperty
	|name='metagen-' metagen=MetagenProperty
	|name='daogen-' daogen=DaogenProperty
	|name='replace-all-regex' WS+ replaceId=IDENT WS+ regex=PropertyValue
	|name='replace-all-replacement' WS+ replaceId=IDENT WS+ replacement=PropertyValue
	) SEMICOLON;

DatabaseProperty:
	(name='is-online'
	|name='is-offline'
	|name='has-url' WS+ dbUrl=PropertyValue
	|name='login-username' WS+ dbUsername=PropertyValue
	|name='login-password' WS+ dbPassword=PropertyValue
	|name='in-catalog' WS+ dbCatalog=DatabaseCatalogAssignement
	|name='active-schema' WS+ dbSchema=DatabaseSchemaAssignement
	|name='jdbc-driver' WS+ (=> COLON dbDriverx=[jvmTypes::JvmType|QualifiedName]|dbDriver=PropertyValue)
	|name='ddl-create' WS+ dbExecuteBefore=PropertyValue
	|name='ddl-drop' WS+ dbExecuteAfter=PropertyValue
	|name='index-types' WS+ dbIndexTypes=PropertyValue
	|name='skip-indexes'
	|name='skip-functions-procedures'
	|name='skip-check-constraints'
	|name='is-of-type' WS+ dbType=DatabaseTypeAssignement
	|name='show-database-info' WS+ dbMetaInfo=DatabaseMetaInfoAssignement
	|name='show-driver-info' WS+ dbDriverInfo=DriverMetaInfoAssignement
	|name='show-driver-output' (WS+ dbMethodsCalls+=DriverMethodOutputAssignement)+
	|name='debug-level' WS+ debug=DebugLevelAssignement
	|name='take-comments'
	|name='lowercase-names'
	|name='uppercase-names'
	);

PojogenProperty:
	(name='types-sqltypes' (WS+ sqlTypes+=SqlTypeAssignement)+
	|name='types-in-table' WS+ dbTable=IDENT (WS+ sqlTypes+=SqlTypeAssignement)+
	|name='types-for-columns' WS+ dbTable=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='types-for-procedure' WS+ dbProcedure=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='types-for-function' WS+ dbFunction=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='show-type-for-column' WS+ dbTable=IDENT WS+ columnType=ShowColumnTypeAssignement
	|name='show-type-for-procedure' WS+ dbProcedure=IDENT WS+ columnType=ShowColumnTypeAssignement
	|name='show-type-for-function' WS+ dbFunction=IDENT WS+ columnType=ShowColumnTypeAssignement
	|name='ignore-tables' (WS+ dbTables+=IDENT)+
	|name='only-tables' (WS+ dbTables+=IDENT)+
	|name='not-abstract-tables' (WS+ dbTables+=IDENT)+
	|name='join-tables' (WS+ joinTables+=JoinTableAssignement)+
	|name='ignore-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='required-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='not-required-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='create-columns' WS+ dbTable=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='rename-tables' (WS+ tables+=TableAssignement)+
	|name='rename-columns' WS+ dbTable=IDENT (WS+ columns+=ColumnAssignement)+
	|name='ignore-one-to-many' WS+ dbTable=IDENT (WS+ exports+=ExportAssignement)+ // exports
	|name='ignore-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='inherit-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='create-one-to-many' WS+ dbTable=IDENT (WS+ exports+=ExportAssignement)+ // exports
	|name='create-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='table-many-to-many' WS+ dbTable=IDENT (WS+ many2s+=ManyToManyAssignement)+
	|name='inherit-discriminator' WS+ dbTable=IDENT WS+ dbColumn=IDENT (WS+ inheritance+=InheritanceAssignement)+
	|name='generate-methods' (WS+ methods+=IDENT)+
	|name='generate-operators' (WS+ operatorsSuffix=IDENT)?
	|name='implements-interfaces' WS+ toImplements=ImplementsAssignement 
	|name='extends-class' WS+ toExtends=ExtendsAssignement
	|name='implements-interfaces-generics' WS+ toImplementsGenerics=ImplementsAssignementGenerics 
	|name='extends-class-generics' WS+ toExtendsGenerics=ExtendsAssignementGenerics
	|name='generate-wrappers'
	|name='generate-validation-annotations'
	|name='preserve-foreign-keys' (WS+ dbTables+=IDENT)*
	|name='implementation-package' WS+ implPackage=IDENT
	|name='make-it-final'
	|name='version-column' WS+ version=IDENT (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?
	|name='debug-level' WS+ debug=DebugLevelAssignement
	|name='pojos-for-procedures' (WS+ procPojos+=ProcedurePojoAssignement)+
	|name='pojos-for-functions' (WS+ funPojos+=FunctionPojoAssignement)+
	|name='active-filter' WS+ activeFilter=PropertyValue
	|name='package' WS+ pckg=PropertyValue
	|name='enum-for-check-constraints' WS+ enumName=IDENT (WS+ dbCheckConstraints+=IDENT)+
	);

MetaTypeAssignement: dbColumn=IDENT '->' type=IDENT ('->' extension=IDENT)?;

MetagenProperty:
	(name='global-sequence' WS+ sequence=IDENT (type=IDENT)? (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?
	|name='table-sequence' WS+ dbTable=IDENT WS+ sequence=IDENT (WS+ type=IDENT)?
	|name='global-identity' (WS+ identity=IDENT (type=IDENT)?)? (WS+ PLUS (WS+ dbTables+=IDENT)+)? (WS+ MINUS (WS+ dbNotTables+=IDENT)+)?
	|name='table-identity' WS+ dbTable=IDENT WS+ identity=IDENT (WS+ type=IDENT)?
	|name='column-meta-type' WS+ dbTable=IDENT (WS+ metaTypes+=MetaTypeAssignement)+
	|name='statement-meta-type' WS+ dbStatement=IDENT (WS+ metaTypes+=MetaTypeAssignement)+
	|name='make-it-final'
	|name='like-columns' (WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+)?
	|name='not-like-columns' (WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+)?
	|name='generate-sequences'
	|name='generate-identities'
	|name='generate-idgenerators'
	|name='generate-default-idgenerators'
	|name='function-result' WS+ dbFunction=IDENT WS+ type=IDENT
	|name='function-result-set' WS+ dbFunction=IDENT WS+ dbTable=IDENT
	|name='procedure-result-set' WS+ dbProcedure=IDENT WS+ dbTable=IDENT
	|name='debug-level' WS+ debug=DebugLevelAssignement
	|name='generate-operators'
	|name='optimize-insert' (WS+ dbTables+=IDENT)*
	|name='optional-features' WS+ dbStatement=IDENT (WS+ optionalFeatures+=IDENT)+
	|name='active-filter' WS+ activeFilter=PropertyValue
	);

DaogenProperty:
	(name='ignore-tables' (WS+ dbTables+=IDENT)+
	|name='only-tables' (WS+ dbTables+=IDENT)*
	|name='implementation-package' WS+ implPackage=IDENT
	|name='implements-interfaces' WS+ toImplements=ImplementsAssignement
	|name='extends-class' WS+ toExtends=ExtendsAssignement
	|name='implements-interfaces-generics' WS+ toImplementsGenerics=ImplementsAssignementGenerics 
	|name='extends-class-generics' WS+ toExtendsGenerics=ExtendsAssignementGenerics
	|name='make-it-final'
	|name='function-result' WS+ dbFunction=IDENT WS+ resultType=PojoType
	|name='debug-level' WS+ debug=DebugLevelAssignement
	|name='active-filter' WS+ activeFilter=PropertyValue
	|name='package' WS+ pckg=PropertyValue
	);

PropertyValue:
  (REST|IDENT_DOT|IDENT|NUMBER|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|DOT|AND|OR|ESC_CHAR)
  (=> (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|DOT|AND|OR|ESC_CHAR)*)
  | STRING_VALUE;

PojoDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'pojo' name=IDENT (class=(IDENT|IDENT_DOT) | COLON classx=[jvmTypes::JvmType|QualifiedName]) SEMICOLON;

TableDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'table' name=IDENT table=IDENT SEMICOLON;

ProcedureDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'procedure' name=IDENT table=IDENT SEMICOLON;

FunctionDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'function' name=IDENT table=IDENT SEMICOLON;

//terminal IDENT_TYPE: IDENT LPAREN NUMBER (COMMA NUMBER)? RPAREN;
terminal IDENT_DOT: IDENT (DOT IDENT)+;
terminal IDENT: ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'=')*;
terminal NUMBER: ('0'..'9')+;
//terminal NEGATIVE_NUMBER: '-'('0'..'9')+;

terminal ESC_CHAR:  '\\' (COLON | SEMICOLON | STRING | LBRACE | RBRACE | BOR | HASH | AT | PERCENT | DOT | '/');
terminal ML_COMMENT : '/*' -> '*/' ;
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')? ;
terminal WS:    (' '|'\t'|'\n'|'\r')+ ;
terminal AND:   '&' '&' ;
terminal OR:    '|' '|' ;

terminal COLON:    ':' ;
terminal SEMICOLON:';' ;
terminal STRING:   '$' ;
terminal COMMA:    ',' ;
terminal MINUS:    '-' ;
terminal PLUS:     '+' ;
terminal LPAREN:   '(' ;
terminal RPAREN:   ')' ;
terminal LBRACE:   '{' ;
terminal RBRACE:   '}' ;
terminal QUESTI:   '?' ;
terminal NOT:      '!' ;
terminal BAND:     '&' ;
terminal BOR:      '|' ;
terminal HASH:     '#' ;
terminal AT:	   '@';
terminal CARET:    '^';
terminal EQUALS:   '=' ;
terminal LESS_THAN:'<' ;
terminal MORE_THAN:'>' ;
terminal PERCENT:  '%' ;
terminal DOT:      '.' ;

terminal REST: !(SEMICOLON);

terminal STRING_VALUE : '\"' -> '\"' ;


FeatureValue:
  (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|AND|OR|ESC_CHAR)+;

//SimpleType:
//	(native = ('_void'|'_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') |
//	(COLON (COLON ref=[PojoEntity|IDENT]) | type=[jvmTypes::JvmType|QualifiedName]));

PojoType:
	(native = ('_void'|'_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') | 
	(COLON (COLON ref=[PojoEntity|IDENT] | type=[jvmTypes::JvmType|QualifiedName]))
	(LESS_THAN (COLON gref=[PojoEntity|IDENT] | gtype=[jvmTypes::JvmType|QualifiedName]) MORE_THAN)?) (array ?= '[]')?;

PackageDeclaration hidden(ML_COMMENT, SL_COMMENT, WS):
	'package' name=(IDENT|IDENT_DOT) ('suffix' suffix=(IDENT|NUMBER))? LBRACE
		elements+=AbstractPojoEntity*
	RBRACE;

Annotation:
	type=[jvmTypes::JvmType|QualifiedName] (COLON COLON COLON features+=AnnotationProperty+)?
;

AnnotationProperty:
	(COMMA)? name=IDENT (COLON (COLON ref=[PojoEntity|IDENT] | type=[jvmTypes::JvmType|QualifiedName]))? (number=NUMBER|value=STRING_VALUE|constant=IDENT)
;

Entity: PojoEntity | EnumEntity;

AnnotatedEntity: (=> AT (=> AT (=> AT (=> AT conflictAnnotations+=Annotation | staticAnnotations+=Annotation) | constructorAnnotations+=Annotation) | annotations+=Annotation))* entity=Entity;

AbstractPojoEntity hidden(ML_COMMENT, SL_COMMENT, WS):
	PackageDeclaration | AnnotatedEntity | Import | Implements | Extends | ImplPackage | PojoDao;

Import hidden(ML_COMMENT, SL_COMMENT, WS):
	'import' importedNamespace = QualifiedNameWithWildcard;

Implements hidden(ML_COMMENT, SL_COMMENT, WS):
	'implements' implements = [jvmTypes::JvmType|QualifiedName] (generics ?= '<<>>')? ('onlyPojos' onlyPojos+=[PojoEntity|IDENT]*)? ('onlyDaos' onlyDaos+=[PojoDao|IDENT]*)? ('exceptPojos' exceptPojos+=[PojoEntity|IDENT]*)? ('exceptDaos' exceptDaos+=[PojoDao|IDENT]*)?;

Extends hidden(ML_COMMENT, SL_COMMENT, WS):
	'extends' extends = [jvmTypes::JvmType|QualifiedName] (generics ?= '<<>>')? ('onlyPojos' onlyPojos+=[PojoEntity|IDENT]*)? ('onlyDaos' onlyDaos+=[PojoDao|IDENT]*)? ('exceptPojos' exceptPojos+=[PojoEntity|IDENT]*)? ('exceptDaos' exceptDaos+=[PojoDao|IDENT]*)?;

ImplPackage hidden(ML_COMMENT, SL_COMMENT, WS):
	'implementation-package' name=(IDENT|IDENT_DOT);

PojoEntityModifier1: ((final?='final') | (abstract?='abstract'));

PojoEntityModifier2: (('extends' superType=[PojoEntity|IDENT]) | ('discriminator' discriminator=(IDENT|NUMBER)) | (operators='operators' (operatorsSuffix=IDENT)?)| ('serializable' sernum=NUMBER));

PojoEntity hidden(ML_COMMENT, SL_COMMENT, WS):
	(modifiers1+=PojoEntityModifier1)* 'pojo' name=IDENT (modifiers2+=PojoEntityModifier2)* 
	LBRACE
		features+=PojoAnnotatedProperty*
	RBRACE;

PojoAnnotatedProperty: (=> AT (=> AT (=> AT setterAnnotations+=Annotation | getterAnnotations+=Annotation) | attributeAnnotations+=Annotation))* feature=PojoProperty;

PojoPropertyModifier: ((required?='required') | (discriminator?='discriminator') | (primaryKey?='primaryKey') | ('index' index=NUMBER) | (version?='optLock') | 
	                   ('updateCol' updateColumn1=IDENT '->' updateColumn2=IDENT) | ('createCol' createColumn1=IDENT '->' createColumn2=IDENT) 
);

PojoProperty hidden(ML_COMMENT, SL_COMMENT, WS):
	name=IDENT (native = ('_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') | 
	(COLON (COLON (COLON (attrs+=[PojoProperty|IDENT])* | ref=[Entity|IDENT]) | type=[jvmTypes::JvmType|QualifiedName]))
	(LESS_THAN (COLON gref=[PojoEntity|IDENT] | gtype=[jvmTypes::JvmType|QualifiedName]) MORE_THAN)?) (array ?= '[]')?
	(modifiers+=PojoPropertyModifier)*
;

EnumEntityModifier1: ((final?='final'));

EnumEntityModifier2: (('extends' superType=[PojoEntity|IDENT]) | ('serializable' sernum=NUMBER));

EnumEntity hidden(ML_COMMENT, SL_COMMENT, WS):
	(modifiers1+=EnumEntityModifier1)* 'enum' name=IDENT (modifiers2+=EnumEntityModifier2)* 
	LBRACE
		features+=EnumProperty*
	RBRACE;

EnumProperty hidden(ML_COMMENT, SL_COMMENT, WS):
	name=IDENT (native = ('_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') | 
	(COLON (COLON COLON value=(NUMBER|STRING_VALUE) | type=[jvmTypes::JvmType|QualifiedName])))
;

PojoDaoModifier: (('extends' superType=[PojoDao|IDENT]) | ('serializable' sernum=NUMBER));

PojoDao hidden(ML_COMMENT, SL_COMMENT, WS):
	(modifiers1+=PojoEntityModifier1)* 'dao' name=IDENT (COLON COLON ((pojoGenerics ?= COLON)?) pojo=[PojoEntity|IDENT])?
	  (modifiers2+=PojoDaoModifier)* 
	LBRACE
		methods+=PojoMethod*
		toInits+=ToInitMethod*
	RBRACE;

PojoMethodModifier: ((callFunction?='callFunction') | (callUpdate?='callUpdate') | (callQuery?='callQuery') | (callQueryFunction?='callQueryFunction') | (callSelectFunction?='callSelectFunction'));

PojoMethod hidden(ML_COMMENT, SL_COMMENT, WS):
		(modifiers1+=PojoMethodModifier)* name=IDENT (type=PojoType COLON COLON COLON (args+=PojoMethodArg)+)?
;

ToInitMethod hidden(ML_COMMENT, SL_COMMENT, WS):
		name=IDENT COLON COLON COLON (args+=PojoMethodArg)+
;

PojoMethodArg hidden(ML_COMMENT, SL_COMMENT, WS):
	name=IDENT type=PojoType
;

QualifiedNameWithWildcard:
  QualifiedName '.*'?; 

QualifiedName: (IDENT|IDENT_DOT);

//     (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|SEMICOLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|AND|OR|ESC_CHAR)
